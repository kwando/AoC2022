# Day 09

```elixir
Mix.install([
  {:vega_lite, "~> 0.1.6"},
  {:kino_vega_lite, "~> 0.1.7"}
])

alias VegaLite, as: Vl
```

## Part 1

--- Day 9: Rope Bridge ---

This rope bridge creaks as you walk along it. You aren't sure how old it is, or whether it can even support your weight.

It seems to support the Elves just fine, though. The bridge spans a gorge which was carved out by the massive river far below you.

You step carefully; as you do, the ropes stretch and twist. You decide to distract yourself by modeling rope physics; maybe you can even figure out where not to step.

Consider a rope with a knot at each end; these knots mark the head and the tail of the rope. If the head moves far enough away from the tail, the tail is pulled toward the head.

Due to nebulous reasoning involving Planck lengths, you should be able to model the positions of the knots on a two-dimensional grid. Then, by following a hypothetical series of motions (your puzzle input) for the head, you can determine how the tail will move.

Due to the aforementioned Planck lengths, the rope must be quite short; in fact, the head (H) and tail (T) must always be touching (diagonally adjacent and even overlapping both count as touching):

```
....
.TH.
....

....
.H..
..T.
....

...
.H. (H covers T)
...
```

If the head is ever two steps directly up, down, left, or right from the tail, the tail must also move one step in that direction so it remains close enough:

````
.....    .....    .....
.TH.. -> .T.H. -> ..TH.
.....    .....    .....

...    ...    ...
.T.    .T.    ...
.H. -> ... -> .T.
...    .H.    .H.
...    ...    ...
```
Otherwise, if the head and tail aren't touching and aren't in the same row or column, the tail always moves one step diagonally to keep up:
```
.....    .....    .....
.....    ..H..    ..H..
..H.. -> ..... -> ..T..
.T...    .T...    .....
.....    .....    .....

.....    .....    .....
.....    .....    .....
..H.. -> ...H. -> ..TH.
.T...    .T...    .....
.....    .....    ....`
You just need to work out where the tail goes as the head follows a series of motions. Assume the head and the tail both start at the same position, overlapping.

For example:

R 4
U 4
L 3
D 1
R 4
D 1
L 5
R 2
This series of motions moves the head right four steps, then up four steps, then left three steps, then down one step, and so on. After each step, you'll need to update the position of the tail if the step means the head is no longer adjacent to the tail. Visually, these motions occur as follows (s marks the starting position as a reference point):

== Initial State ==

......
......
......
......
H.....  (H covers T, s)

== R 4 ==

......
......
......
......
TH....  (T covers s)

......
......
......
......
sTH...

......
......
......
......
s.TH..

......
......
......
......
s..TH.

== U 4 ==

......
......
......
....H.
s..T..

......
......
....H.
....T.
s.....

......
....H.
....T.
......
s.....

....H.
....T.
......
......
s.....

== L 3 ==

...H..
....T.
......
......
s.....

..HT..
......
......
......
s.....

.HT...
......
......
......
s.....

== D 1 ==

..T...
.H....
......
......
s.....

== R 4 ==

..T...
..H...
......
......
s.....

..T...
...H..
......
......
s.....

......
...TH.
......
......
s.....

......
....TH
......
......
s.....

== D 1 ==

......
....T.
.....H
......
s.....

== L 5 ==

......
....T.
....H.
......
s.....

......
....T.
...H..
......
s.....

......
......
..HT..
......
s.....

......
......
.HT...
......
s.....

......
......
HT....
......
s.....

== R 2 ==

......
......
.H....  (H covers T)
......
s.....

......
......
.TH...
......
s.....
After simulating the rope, you can count up all of the positions the tail visited at least once. In this diagram, s again marks the starting position (which the tail also visited) and # marks other positions the tail visited:

..##..
...##.
.####.
....#.
s###..
So, there are 13 positions the tail visited at least once.

Simulate your complete hypothetical series of motions. How many positions does the tail of the rope visit at least once?
````

```elixir
dir_map = %{
  "R" => {1, 0},
  "L" => {-1, 0},
  "D" => {0, -1},
  "U" => {0, 1}
}

instructions =
  File.stream!(Path.join(__DIR__, "input.txt"))
  |> Stream.map(&String.trim/1)
  |> Stream.map(fn line ->
    [dir, steps] =
      line
      |> String.trim()
      |> String.split(" ", parts: 2)

    {dir_map[dir], String.to_integer(steps)}
  end)

:ok
```

```elixir
defmodule RopeSimulator do
  def simulate(instructions, knots \\ 2) do
    {_final_pos, trail} =
      for i <- instructions, reduce: {{0, 0}, []} do
        {pos, trail} ->
          new_trail = move(pos, i, [])
          new_pos = hd(new_trail)
          {new_pos, new_trail ++ trail}
      end

    trail = Enum.reverse(trail)

    for _ <- 2..knots, reduce: trail do
      trail -> Enum.scan(trail, {0, 0}, fn head, tail -> move_tail(head, tail) end)
    end
  end

  def move({x, y}, {_dir, 0}, trail), do: [{x, y} | trail]

  def move(pos, {dir, n}, trail) do
    new_pos = translate(pos, dir)
    move(new_pos, {dir, n - 1}, [new_pos | trail])
  end

  # overlap, do nothing
  def move_tail({x, y}, {x, y}), do: {x, y}
  # on same row
  def move_tail({hx, y}, {tx, y}) when abs(hx - tx) == 1, do: {tx, y}
  def move_tail({hx, y}, {tx, y}) when hx > tx, do: {tx + 1, y}
  def move_tail({hx, y}, {tx, y}) when hx < tx, do: {tx - 1, y}
  # on same column
  def move_tail({x, hy}, {x, ty}) when abs(hy - ty) == 1, do: {x, ty}
  def move_tail({x, hy}, {x, ty}) when hy > ty, do: {x, ty + 1}
  def move_tail({x, hy}, {x, ty}) when hy < ty, do: {x, ty - 1}
  # somewhere diagonally => do nothing
  def move_tail({hx, hy}, {tx, ty}) when abs(hx - tx) + abs(hy - ty) == 2, do: {tx, ty}

  # move tail closer diagonally
  def move_tail({hx, hy}, {tx, ty}) do
    {dx, dy} = {hx - tx, hy - ty}
    translate({tx, ty}, {round(dx / abs(dx)), round(dy / abs(dy))})
  end

  defp translate({x, y}, {dx, dy}), do: {x + dx, y + dy}
end
```

```elixir
instructions
|> RopeSimulator.simulate(2)
|> Enum.uniq()
|> Enum.count()
```

```elixir
instructions
|> RopeSimulator.simulate(10)
|> Enum.uniq()
|> Enum.count()
```
