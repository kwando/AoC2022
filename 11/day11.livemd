# Day 11

## Part 1

```elixir
input =
  Path.join(__DIR__, "input.txt")
  |> File.stream!()
  |> Stream.map(&String.trim/1)
  |> Enum.chunk_every(7)
```

```elixir
defmodule Monkey do
  defstruct [:id, :items, :operation, :test, :when_true, :when_false, inspections: 0]

  def parse(lines) do
    for line <- lines, reduce: %__MODULE__{} do
      monkey ->
        parse(monkey, line)
    end
  end

  defp parse(monkey, "Monkey " <> n) do
    {n, ":"} = Integer.parse(n)
    %{monkey | id: n}
  end

  defp parse(monkey, "Starting items: " <> items) do
    items = String.split(items, ",") |> Enum.map(&(&1 |> String.trim() |> String.to_integer()))
    %{monkey | items: Enum.reverse(items)}
  end

  defp parse(monkey, "Operation: new = " <> op) do
    [_, operand1, op, operand2] = Regex.run(~r/(\w+)\s([+*])\s(\w+)/, op)
    operation = {parse_operation(op), parse_operand(operand1), parse_operand(operand2)}
    %{monkey | operation: operation}
  end

  defp parse(monkey, "Test: divisible by " <> number) do
    factor = String.to_integer(number)
    %{monkey | test: &(rem(&1, factor) == 0)}
  end

  defp parse(monkey, "If true: throw to monkey " <> number) do
    %{monkey | when_true: String.to_integer(number)}
  end

  defp parse(monkey, "If false: throw to monkey " <> number) do
    %{monkey | when_false: String.to_integer(number)}
  end

  defp parse(monkey, ""), do: monkey

  defp parse_operand("old"), do: :old
  defp parse_operand(number), do: String.to_integer(number)
  defp parse_operation("*"), do: :multiply
  defp parse_operation("+"), do: :add
end

monkeys =
  for data <- input, into: %{} do
    monkey = Monkey.parse(data)
    {monkey.id, monkey}
  end
```

```elixir
defmodule MonkeyBusiness do
  def rounds(monkeys, n) do
    for _ <- 1..n, reduce: monkeys do
      monkeys ->
        run_round(monkeys)
        |> tap(&print(&1))
    end
  end

  def print(monkeys) do
    for n <- 0..(map_size(monkeys) - 1) do
      monkey = monkeys[n]
      [to_string(monkey.id), ": ", monkey.items |> Enum.map(&to_string/1) |> Enum.join(", "), ?\n]
    end
    |> IO.puts()
  end

  def run_round(monkeys) do
    for n <- 0..(map_size(monkeys) - 1), reduce: monkeys do
      monkeys ->
        monkey = monkeys[n]
        # IO.inspect(monkey.items, label: "Monkey #{monkey.id}")
        for item <- Enum.reverse(monkey.items), reduce: monkeys do
          monkeys ->
            wl = operate(monkey.operation, item)
            new_wl = floor(wl / 3)

            next_monkey =
              if monkey.test.(new_wl) do
                monkey.when_true
              else
                monkey.when_false
              end

            # IO.inspect({monkey.id, item, wl, new_wl, next_monkey})
            monkeys
            |> get_and_update_in([next_monkey, Access.key!(:items)], &{&1, [new_wl | &1]})
            |> elem(1)
        end
        |> put_in([monkey.id, Access.key!(:items)], [])
        |> get_and_update_in(
          [monkey.id, Access.key!(:inspections)],
          &{&1, &1 + Enum.count(monkey.items)}
        )
        |> elem(1)
    end
  end

  defp operate({:multiply, op1, op2}, value), do: operand(op1, value) * operand(op2, value)
  defp operate({:add, op1, op2}, value), do: operand(op1, value) + operand(op2, value)
  defp operand(:old, old), do: old
  defp operand(number, _) when is_integer(number), do: number
end

MonkeyBusiness.rounds(monkeys, 20)
|> Map.values()
|> get_in([Access.all(), Access.key!(:inspections)])
|> Enum.sort(:desc)
|> Enum.take(2)
|> Enum.reduce(&(&1 * &2))
```

## Part 2

```elixir
rem(2080, 13) === 0
```
